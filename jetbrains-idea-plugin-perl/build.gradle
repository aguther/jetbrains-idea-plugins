// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '4.0.0'
    id "de.undercouch.download" version '3.1.1'
}

// --------------------------------------------------------------------------------------------------------------------
task downloadPackages(type: de.undercouch.gradle.tasks.download.Download) {
    src "http://plugins.jetbrains.com/plugin/download?pr=idea_ce&updateId=${ideaPluginUpdateId}"
    dest project.file("$buildDir/downloaded/plugin.zip")
    onlyIfNewer true
}

// --------------------------------------------------------------------------------------------------------------------
def extractionDirectory = project.file("$buildDir/extracted")

task extractPackages(dependsOn: downloadPackages) {

    outputs.dir extractionDirectory

    doLast {
        copy {
            from zipTree(downloadPackages.dest)
            into extractionDirectory
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = '/opt/jetbrains/idea'

ospackage {
    vendor = 'Alexandr Evstigneev'
    url = 'https://github.com/hurricup/Perl5-IDEA'
    license = 'http://www.apache.org/licenses/LICENSE-2.0'

    summary = 'Perl5 support to any Java-based JetBrains product.'
    description = 'Camelcade adds Perl5 support to any Java-based JetBrains product.'

    packageGroup = 'Development/Tools'

    packager = 'Andreas Guther'

    version = ideaPluginVersion
    release = rpmRelease
    arch = NOARCH
    os = LINUX

    user = 'root'

    requires('jetbrains-idea', ideaMinVersion, GREATER | EQUAL)
    //requires('jetbrains-idea', ideaMaxVersion, LESS)

    into "$targetDirectory"

    from("$extractionDirectory") {
        into 'plugins'
    }
}

buildRpm {
    dependsOn extractPackages
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'clean', 'buildRpm'
