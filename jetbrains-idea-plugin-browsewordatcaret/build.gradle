// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '4.0.0'
    id "de.undercouch.download" version '3.1.1'
    id 'net.researchgate.release' version '2.3.5'
}

// --------------------------------------------------------------------------------------------------------------------
task downloadPackages(type: de.undercouch.gradle.tasks.download.Download) {
    src "http://plugins.jetbrains.com/plugin/download?pr=idea_ce&updateId=${ideaPluginUpdateId}"
    dest project.file("$buildDir/downloaded/BrowseWordAtCaret.jar")
    onlyIfNewer true
}

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = '/opt/jetbrains/idea'

ospackage {
    vendor = 'Minas Manthos'
    url = 'https://github.com/minman/browse-word-at-caret'
    license = 'http://www.apache.org/licenses/LICENSE-2.0'

    summary = 'Easily browse next/previous word at caret for IntelliJ.'
    description = 'This plugin allows to easily browse next/previous word at caret and highlight other appearances of selected word.'

    packageGroup = 'Development/Tools'

    packager = 'Andreas Guther'

    version = ideaPluginVersion
    release = rpmRelease
    arch = NOARCH
    os = LINUX

    user = 'root'

    requires('jetbrains-idea', ideaMinVersion, GREATER | EQUAL)
    //requires('jetbrains-idea', ideaMaxVersion, LESS)

    into "$targetDirectory"

    from(downloadPackages.dest) {
        into 'plugins/browsewordatcaret/lib'
    }
}

buildRpm {
    dependsOn downloadPackages
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'clean', 'buildRpm'