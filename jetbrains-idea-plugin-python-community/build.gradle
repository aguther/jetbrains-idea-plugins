// --------------------------------------------------------------------------------------------------------------------
plugins {
    id 'nebula.ospackage' version '3.5.0'
    id "de.undercouch.download" version '2.1.0'
    id 'net.researchgate.release' version '2.3.5'
}

// --------------------------------------------------------------------------------------------------------------------
task downloadPackages(type: de.undercouch.gradle.tasks.download.Download) {
    src "http://plugins.jetbrains.com/plugin/download?pr=idea_ce&updateId=${ideaPluginUpdateId}"
    dest project.file("$buildDir/downloaded/plugin.zip")
    onlyIfNewer true
}

// --------------------------------------------------------------------------------------------------------------------
def extractionDirectory = project.file("$buildDir/extracted")

task extractPackages(dependsOn: downloadPackages) {

    outputs.dir extractionDirectory

    doLast {
        copy {
            from zipTree(downloadPackages.dest)
            into extractionDirectory
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
def targetDirectory = '/opt/jetbrains/idea'

ospackage {
    vendor = 'JetBrains'
    url = 'https://confluence.jetbrains.com/display/PYH/'
    license = 'Apache 2.0'

    summary = 'The Python plug-in provides smart editing for Python scripts.'
    description = 'The Python plug-in provides smart editing for Python scripts. The feature set of the plugin corresponds to PyCharm IDE Community Edition.'

    packageGroup = 'Development/Tools'

    packager = 'Andreas Guther'

    version = ideaPluginVersion
    release = rpmRelease
    arch = NOARCH
    os = LINUX

    user = 'root'

    requires('jetbrains-idea', ideaMinVersion, GREATER | EQUAL)
    //requires('jetbrains-idea', ideaMaxVersion, LESS)

    into "$targetDirectory"

    from("$extractionDirectory") {
        into 'plugins'
    }
}

buildRpm {
    dependsOn extractPackages
}

// --------------------------------------------------------------------------------------------------------------------
defaultTasks 'clean', 'buildRpm'
